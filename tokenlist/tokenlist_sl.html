<html>
<head>
   <title></title>
   <meta http-equiv="Content-Type" content="text/html; charset=ascii">
   <meta http-equiv="Author" content="Marko Mahnic">
<style>
  BODY { width: 720px }
  PRE {background:#e0e0ff; padding: 8px;}
</style>
</head>
<body>
<h3>Tokenlist installation</h3>

First you have to copy <code>tokenlist.sl</code> somewhere on your jed_library_path.
Optionally you can also copy <code>tkl-modes.sl</code> if you will use the
default settings for some modes (SLang, C, python, PHP, HTML, LaTeX).

Insert the content of the INITIALIZATION block (from <code>tokenlist.sl</code>) or just
<pre>
  require("tokenlist");
</pre>

into your jed.rc (or .jedrc) file (or use the "make_ini" and "home-lib"
modes from jedmodes.sf.net). Optionally add some keybindings, e.g.:

<pre>
  setkey("list_routines", "^R");
  setkey("occur", "^O");
</pre>

The function list_routines is very simple in its default implementation
and is conditionally useful for C and SLang mode. To extend its capabilities you
have to add some macros to search for routines and extract them into the
list-buffer. Extensions are provided for more modes in the file <code>tkl-modes.sl</code> 
which can be loaded in <code>tokenlist_hook()</code> which you define in 
<code>.jedrc (jed.rc)</code>:

<pre>
  define tokenlist_hook()
  {
     if (expand_jedlib_file("tkl-modes"))
       require("tkl-modes");
  }
</pre>

If you use <code>require("tokenlist")</code> to load <code>tokenlist.sl</code>
then you should put the definition of <code>tokenlist_hook()</code> <b>before</b>
<code>require("tokenlist")</code>.

<p>
You can add more definitions to <code>tkl-modes.sl</code> 
but it is better to write a separate file like <code>mytklist.sl</code> and 
change <code>tokenlist_hook()</code> to:

<pre>
  define tokenlist_hook()
  {
     if (expand_jedlib_file("tkl-modes"))
       require("tkl-modes");
     if (expand_jedlib_file("mytklist"))
       require("mytklist");
  }
</pre>


<h3>Defining your own routine-search</h3>

You can write the macros for routine-search directly into <code>.jedrc</code>
but it is better to add them to a separate file so they will be loaded only
when needed. Here is a minimal example for <code>mytklist.sl</code>:

<pre>
% ----------- mytklist.sl ------
provide("mytklist");
</pre>

Let us now define a special X-TEXT mode where the titles on the first level
start with '::', on the second with '==' an and on the third level with
'--'. In the result list the titles should be indented depending on
the level of the title.

<p>
For the new mode to work we have to define the function
<code>x_text_list_routines_setup(opt)</code> where <code>opt</code> is a structure
that defines the behaviour of <code>list_routines</code>. It has the following
fields:

<p>
<table border=1 cellspacing=0 cellpadding=2>
  <tr><th>Field</th><th>Description</th><th>Default</th></tr>
  <tr><td><code>mode</code></td><td>
  The name of the mode.
  </td>
  <td>&nbsp;</td></tr>
  
  <tr><td><code>list_regexp</code></td><td>
  A list of regular expressions to search for. <code>list_routines</code> will
  take every item from the list and search for all the occurences in the
  active buffer that match the item (regular expression). After an expression
  is found, some text is extracted from the buffer with a call to <code>fn_extract</code>.
  <p>
  An item from the list can be a function reference instead of a regular expression.
  In this case the function will be called by <code>list_routines</code> until
  it returns 0. The function takes an integer parameter which is the current
  index and looks like this:
<pre>
    % Int_Type searc_fn(Int_Type array_index)
    define searc_fn(idx)
    {
       return fsearch("something");
    }
</pre>
  </td>
  <td><code>{"^[a-zA-Z].*("};</code></td></tr>
  
  <tr><td><code>fn_extract</code></td><td>
  A reference to a (private) function that extracts some text at the current point in the 
  active buffer and returns it as a string.
  It is called after every match that <code>list_routines</code> finds.
  The function takes an integer parameter which is the current
  index and looks like this:
<pre>
    % String extract_fn(Int_Type array_index)
    define extract_fn(idx)
    {
       return line_as_string();
    }
</pre>
  </td>
  <td>
  <code>&amp;_list_routines_extract</code>
  <p>
  The function is defined in tokenlist and returns the current line (line_as_string).
  </td></tr>
  
  <tr><td><code>onlistcreated</code></td><td>
  A reference to a (private) function that is called after all the items from <code>list_regexp</code>
  have been processed. The active buffer is the list of matches and it is narrowed to the
  matches found in the last buffer.
  <p>
  You can use this hook to sort the matched lines or do any other processing of the
  buffer. There are two helper functions defined in <code>tokenlist.sl</code> to
  sort the lines by line number (<code>tkl_sort_by_line</code>) or 
  by content (<code>tkl_sort_by_value</code>).
  </td>
  <td><code>NULL</code></td></tr>
</table>

<p>
Here is the code for the "X-TEXT" mode:

<pre>
% ----------- mytklist.sl ------
autoload("tkl_sort_by_value", "tokenlist");
autoload("tkl_sort_by_line", "tokenlist");
provide("mytklist");

private define x_text_list_routines_extract(n)
{
  variable chars  = [":", "=", "-"];
  variable prefix = ["", "..", "...."];
  if (n < 3) {
    bol(); 
    skip_chars(chars[n]); 
    push_mark();
    eol();
    return (prefix[n] + strtrim(bufsubstr()));
  }
  return "";
}

define x_text_list_routines_setup(opt)
{
  opt.list_regexp = {
    '^::',
    '^==',
    '^--'
  };
  opt.fn_extract = &x_text_list_routines_extract;
  opt.onlistcreated = &tkl_sort_by_line;
}

</pre>


<p>
If have a buffer TestBufer.xtxt with the content like

<pre>
::Heading 1
...
==Heading 2
...
--Heading 3
..
==Heading 4
</pre>

<code>list_routines</code> would generate the following list:

<pre>
Buffer: TestBufer.xtxt
     3 : Heading 1
    27 : ..Heading 2
    33 : ....Heading 3
    43 : ..Heading 4
</pre>

You can see that the list is sorted by line number. This is because we set 
<code>opt.onlistcreated</code> to <code>&amp;tkl_sort_by_line</code>. If
we set it to <code>NULL</code> we get the following list:

<pre>
Buffer: TestBufer.xtxt
     3 : Heading 1
    27 : ..Heading 2
    43 : ..Heading 4
    33 : ....Heading 3
</pre>

If we wanted to sort the titles by value, we could use
<code>tkl_sort_by_value</code>. Unfortunately it would not sort the titles
alphabetically because the titles in the buffer are indented.

<p>
<B>Mode writers</B> are encouraged to add <code>_list_routines_setup</code>
definitions to their modes.

<h3>Extending existing modes</h3>

Suppose you would like to list all the special blocks like
<code>#&lt;INITIALIZATION&gt;</code> in SLang mode. You could change the 
function <code>slang_list_routines_setup</code> in <code>tkl-modes.sl</code> or copy the
contents from <code>tkl-modes.sl</code> to <code>mytklist.sl</code> and change it there. This
would be hard to maintain. 

<p>
That is why another hook was added to tokenlist:

<pre>
  Void  tokenlist_routine_setup_hook(opt)
</pre>

This hook is not defiend by default. You can add it to a file like
<code>mytklist.sl</code>. In our example we add a new regular expression and
modify the extraction function. We store some values from
the structure <code>opt</code> for later use.

<pre>
private variable slang_nregex = NULL;
private variable slang_fn_extract = NULL;
private variable slang_onlistcreated = NULL;

private define my_slang_fn_extract(idx)
{
   if (idx < slang_nregex && slang_fn_extract != NULL)
      return (@slang_fn_extract)(idx);
   else
      return strtrim(line_as_string());
}
  
define tokenlist_routine_setup_hook(opt)
{
   if (opt.mode == "slang")
   {
      slang_nregex = length(opt.list_regexp);
      slang_fn_extract = opt.fn_extract;
      slang_onlistcreated = opt.onlistcreated;
      list_append(opt.list_regexp, "^#\\<[a-zA-Z]");
      opt.fn_extract = &amp;my_slang_fn_extract;
   }
}
</pre>

<code>my_slang_fn_extract</code> will use the original extraction function for
the original regular expressions and extract the current line for the new
regular expression.

<P>
Imagine that you woud like to sometimes list only functions and at other
times only variables. You would define a global variable and prepare the
members of the parameter <code>opt</code> according to the value of the variable:

<pre>
% 0 - all; 1 - variables; 2 - macros
variable slang_routine_mode = 0;

private define my_slang_extract_variable(n)
{
  return(strtrim(line_as_string()));
}

private define my_slang_extract_macro(n)
{
  return(strtrim(line_as_string()));
}

define tokenlist_routine_setup_hook(opt)
{
   if (opt.mode == "slang")
   {
      if (slang_routine_mode == 1)
      {
         opt.list_regexp = { "^variable[ \t]" };
         opt.fn_extract = &my_slang_extract_variable;
      }
      if (slang_routine_mode == 2)
      {
         opt.list_regexp = { "^define[ \t]" };
         opt.fn_extract = &my_slang_extract_macro;
      }
   }
}
</pre>

<h3>Future</h3>

The structure <code>opt</code> could have more members so tokenlist could
be made extremely configurable:

<ul>
  <li> what to do when user presses Enter (replacing tkl_goto_token)
  <li> what to do when user presses Space (replacing tkl_display_token)
  <li> what other keybindings do we want in the list
  <li> what to do when the current line is changed in the list (extending
  the tkl_update_token_hook)
  <li> ...
</ul>
  
</body>
</html>
